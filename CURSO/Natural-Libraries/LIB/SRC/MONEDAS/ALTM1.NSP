* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
DEFINE DATA LOCAL
    1 VISTA-MONEDA VIEW OF MONEDAS
        2 COD-MONEDA
        2 DESC
*
    1 #COD-MONEDA        (A03)
    1 #DESC              (A20)
    1 #ENC               (L)
END-DEFINE
*
DEFINE WINDOW AVISO
  SIZE 7 * 50
  BASE 10 / 30 
  FRAMED
*
* ---- MAIN ----
*
SET KEY ALL
REPEAT
    INPUT USING MAP 'MMON1'

    DECIDE ON FIRST VALUE *PF-KEY
        VALUE 'ENTR'
            IF #COD-MONEDA EQ ' '
                REINPUT 'DEBE INGRESAR UN CODIGO DE MONEDA'
            END-IF

            IF #DESC EQ ' '
                REINPUT 'DEBE INGRESAR UNA DESCRIPCION'
            END-IF

            PERFORM VALIDAR-EXISTENCIA

            IF #ENC EQ FALSE
                PERFORM GUARDAR-REGISTRO
            ELSE
                PERFORM MENSAJE-EXISTE
            END-IF

            RESET #COD-MONEDA #DESC

        VALUE 'PF3'
            ESCAPE BOTTOM

        NONE VALUE
            IGNORE
    END-DECIDE
END-REPEAT
*
FETCH 'MENU1'
*
* ---- SUBRUTINAS ----
*
DEFINE SUBROUTINE VALIDAR-EXISTENCIA
    #ENC := FALSE
    FIND VISTA-MONEDA WITH VISTA-MONEDA.COD-MONEDA = #COD-MONEDA
         #ENC := TRUE
    END-FIND
END-SUBROUTINE
*
DEFINE SUBROUTINE GUARDAR-REGISTRO
    VISTA-MONEDA.COD-MONEDA := #COD-MONEDA
    VISTA-MONEDA.DESC       := #DESC
    STORE VISTA-MONEDA
    END TRANSACTION
    SET WINDOW 'AVISO'
    INPUT 'EL REGISTRO HA SIDO AGREGADO'
    SET WINDOW OFF
END-SUBROUTINE
*
DEFINE SUBROUTINE MENSAJE-EXISTE
    SET WINDOW 'AVISO'
    INPUT 'EL REGISTRO YA EXISTE'
    SET WINDOW OFF
END-SUBROUTINE
*
END

