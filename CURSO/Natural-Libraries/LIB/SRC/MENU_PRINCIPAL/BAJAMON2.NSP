* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
DEFINE DATA LOCAL
    1 VISTA-MONEDA VIEW OF MONEDAS
        2 COD-MONEDA
        2 DESC
*
    1 #COD-MONEDA        (A03)
    1 #ISN               (P10)
    1 #RESPUESTA         (A01)
END-DEFINE
*
* ---- MAIN ----
*
DEFINE WINDOW AVISO
  SIZE 7  * 50
  BASE 10 / 30 
  FRAMED
*
SET KEY ALL
*
REPEAT
    INPUT 'INGRESE CODIGO MONEDA:' #COD-MONEDA / 
    RESET #RESPUESTA 
     DECIDE ON FIRST VALUE *PF-KEY
    VALUE 'ENTR'
      PERFORM INGRESAR-CODIGO
      PERFORM BUSCAR-MONEDA
            DECIDE ON FIRST VALUE #RESPUESTA
         VALUE 'S'
           GE. GET VISTA-MONEDA #ISN
           DELETE (GE.)
           END TRANSACTION
           RESET   #COD-MONEDA #RESPUESTA
*
         VALUE 'N'
          INPUT NO ERASE 'SE CANCELÓ LA ELIMINACIÓN DEL REGISTRO'
          RESET   #COD-MONEDA #RESPUESTA
*
         NONE VALUE
          REINPUT 'INGRESE UNA OPCIÓN'
       END-DECIDE
*      
    VALUE 'PF3'
      ESCAPE BOTTOM
*      
    NONE VALUE
      IGNORE
  END-DECIDE        
END-REPEAT
*
FETCH 'MENU1'
*
* ---- SUBRUTINAS ----
*
DEFINE SUBROUTINE INGRESAR-CODIGO
*
          IF #COD-MONEDA EQ ' '
              REINPUT 'INGRESE CODIGO MONEDA'
          END-IF                     
END-SUBROUTINE
*
DEFINE SUBROUTINE BUSCAR-MONEDA
*
FD. FIND VISTA-MONEDA WITH VISTA-MONEDA.COD-MONEDA = #COD-MONEDA
*
        IF NO RECORD FOUND           
                REINPUT 'EL REGISTRO NO EXISTE - INGRESE CODIGO MONEDA'           
        END-NOREC
*        
        #ISN := *ISN(FD.)     
*
    END-FIND
*        
            SET WINDOW 'AVISO'
                INPUT 'CODIGO MONEDA:' VISTA-MONEDA.COD-MONEDA(AD=O) /
                      'DESCRIPCION:'   VISTA-MONEDA.DESC(AD=O) /        
                      '¿DESEA ELIMINAR EL REGISTRO? (S/N):' #RESPUESTA(AD=M)
            SET WINDOW OFF           
*               
END-SUBROUTINE
*
END